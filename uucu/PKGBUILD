# Maintainer:
# Contributor: Sam <dev at samarthj dot com>
# Contributor: √Årni Dagur <arnidg@protonmail.ch>

pkgname="uutils-coreutils-git"
pkgver=0.0.27.r279.gdb40287
pkgrel=1
pkgdesc="GNU Coreutils rewritten in Rust"
url='https://github.com/uutils/coreutils'
license=('MIT')
arch=('x86_64')

depends=(
  'gcc-libs'
  'glibc'
  'oniguruma'
)
makedepends=(
  'cargo'
  'git'
  'python-sphinx'
  'rust'
)

options=(!lto !debug)

provides=("coreutils")
conflicts=("coreutils")

_pkgsrc="$pkgname"
source=("$_pkgsrc"::"git+https://github.com/uutils/coreutils.git")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgsrc"
  git describe --long --tags --abbrev=7 \
    | sed -E 's/^[^0-9]*//;s/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
    cd "$_pkgsrc"
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  export RUSTONIG_DYNAMIC_LIBONIG=1

  cd "$_pkgsrc"
  make PROFILE=release
  cargo build --frozen --release --features "[ b2sum base32 base64 basename basenc cat chcon chgrp chmod chown chroot cksum comm cp csplit cut date dd df dir dircolors dirname du echo env expand expr factor false fmt fold head hostid id install join link ln logname ls md5sum mkdir mkfifo mknod mktemp mv nice nl nohup nproc numfmt od paste pathchk pinky pr printenv printf ptx pwd readlink realpath rm rmdir runcon seq sha1sum sha224sum sha256sum sha384sum sha512sum shred shuf sleep sort split stat stdbuf stty sum sync tac tail tee test timeout touch tr true truncate tsort tty uname unexpand uniq unlink users vdir wc who whoami yes" --no-default-features
}

package() {
  cd "$_pkgsrc"
  
      find target/release \
        -maxdepth 1 \
        -executable \
        -type f \
        -exec install -Dm0755 -t "$pkgdir/usr/bin/" {} +
    
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
