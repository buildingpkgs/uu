# Maintainer: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=uutils-util-linux-git
pkgver=0.0.1.r311.gdd76c2a
pkgrel=1
pkgdesc="Rust reimplementation of the util-linux project"
arch=('i686' 'x86_64')
url="https://github.com/uutils/util-linux"
license=('MIT')
depends=('gcc-libs')
makedepends=('git' 'cargo')
provides=("util-linux")
conflicts=('util-linux')
source=("git+https://github.com/uutils/util-linux.git")
sha256sums=('SKIP')


prepare() {
  cd "util-linux"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

pkgver() {
  cd "util-linux"

  _tag=$(git tag -l --sort -v:refname | grep -E '^v?[0-9\.]+$' | head -n1)
  _rev=$(git rev-list --count $_tag..HEAD)
  _hash=$(git rev-parse --short HEAD)
  printf "%s.r%s.g%s" "$_tag" "$_rev" "$_hash" | sed 's/^v//'
}

package() {
  cd "util-linux"

  cargo build --frozen --release --all-features
  find target/release \
        -maxdepth 1 \
        -executable \
        -type f \
        -exec install -Dm0755 -t "$pkgdir/usr/bin/" {} +

  install -Dm644 "LICENSE" -t "$pkgdir/usr/share/licenses/uutils-util-linux"
}
