# Maintainer: Chocobo1 <chocobo1 AT archlinux DOT net>

: ${CARGO_HOME:=$SRCDEST/cargo-home}
: ${CARGO_TARGET_DIR:=target}
: ${RUSTUP_TOOLCHAIN:=stable}

export CARGO_HOME CARGO_TARGET_DIR RUSTUP_TOOLCHAIN

pkgname=uutils-findutils-git
pkgver=0.7.0.r17.g50b7ec6
pkgrel=1
pkgdesc="Rust implementation of findutils"
arch=('i686' 'x86_64')
url="https://github.com/uutils/findutils"
license=('MIT')
depends=('gcc-libs' 'oniguruma')
makedepends=('git' 'cargo')
provides=("findutils")
conflicts=("findutils")
source=("git+https://github.com/uutils/findutils.git")
sha256sums=('SKIP')
options=(!debug)

prepare() {
  cd "findutils"

  if [ ! -f "Cargo.lock" ]; then
    cargo update
  fi
  cargo fetch
  export RUSTONIG_DYNAMIC_LIBONIG=1
}

pkgver() {
  cd "findutils"

  _tag=$(git tag -l --sort -v:refname | grep -E '^v?[0-9\.]+$' | head -n1)
  _rev=$(git rev-list --count $_tag..HEAD)
  _hash=$(git rev-parse --short HEAD)
  printf "%s.r%s.g%s" "$_tag" "$_rev" "$_hash" | sed 's/^v//'
}

package() {
  cd "findutils"

  cargo install \
    --locked \
    --no-track \
    --root "$pkgdir/usr" \
    --path .
  rm "$pkgdir"/testing-commandline
  install -Dm644 "LICENSE" -t "$pkgdir/usr/share/licenses/uutils-findutils"
}
